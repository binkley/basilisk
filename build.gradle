import com.github.spotbugs.SpotBugsTask

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.11"
    }
}

plugins {
    id "build-dashboard"
    id "java"
    id "com.dorongold.task-tree" version "1.3.1"
    id "org.springframework.boot" version "2.1.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "org.unbroken-dome.test-sets" version "2.0.3"
    id "com.github.spotbugs" version "1.6.8"
    id "jacoco"
}

group = "hm.binkley"
version = "0"

testSets {
    databaseTest {
        shouldRunAfter test
    }
    integrationTest {
        shouldRunAfter test
    }
    liveTest {
        shouldRunAfter test
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor "org.projectlombok:lombok"

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    compileOnly "org.projectlombok:lombok"
    compileOnly "com.github.spotbugs:spotbugs-annotations:3.1.10"

    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-hateoas"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.zalando:problem-spring-web:0.23.0"
    implementation "io.springfox:springfox-swagger2:2.9.2"

    runtime "org.springframework.data:spring-data-rest-hal-browser"
    runtime "com.h2database:h2"
    runtime "io.springfox:springfox-swagger-ui:2.9.2"

    testCompileOnly "org.projectlombok:lombok"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:3.1.10"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude module: "junit"
    }
    testImplementation "org.springframework.boot:spring-boot-starter-webflux"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.awaitility:awaitility:3.1.5"

    testRuntime "org.junit.jupiter:junit-jupiter-engine"
}

sourceCompatibility = 11
targetCompatibility = 11

spotbugs {
    toolVersion = "3.1.10"
}

jacoco {
    toolVersion = "0.8.2"
}

defaultTasks "build"

tasks.withType(JavaCompile) {
    options.compilerArgs = [
            "-encoding", "UTF8",
            "-parameters",
            "-Werror",
            "-Xdiags:verbose",
            "-Xlint:all,-processing,-serial"
    ]
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat "full"
    }
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(JacocoReportBase) {
    executionData(fileTree(buildDir).include("/jacoco/*.exec"))
}

tasks {
    wrapper {
        distributionType = Wrapper.DistributionType.ALL
    }

    clean {
        delete "out"
    }

    jacocoTestReport {
        dependsOn tasks.withType(Test)
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = new BigDecimal("1.00")
                }
            }
        }
        dependsOn(jacocoTestReport)
    }

    check {
        dependsOn(jacocoTestCoverageVerification)

        finalizedBy {
            buildDashboard
        }
    }

    springBoot {
        buildInfo()
    }

    bootRun {
        systemProperties = System.properties as Map<String, ?>
    }
}
