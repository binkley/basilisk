apply plugin: "org.springframework.boot"
apply plugin: "org.flywaydb.flyway"

testSets {
    integrationTest { extendsFrom unitTest }
    databaseTest { extendsFrom integrationTest }
    liveTest { extendsFrom databaseTest }
}

configurations {
    flywayMigration

    all*.exclude module: 'junit'
    all*.exclude module: 'slf4j-simple'
}

flyway {
    url = "jdbc:postgresql://localhost:5432/basilisk"
    user = "basilisk"
    password = ""
    // NB - Postgres is case-insensitive unless quoted
    // The migration scripts are not quoting, but Flyway plugin does quote,
    // so match the expected case in the DB
    schemas = ["flora", "x"]
    configurations = ["flywayMigration"]
}

dependencies {
    flywayMigration "org.postgresql:postgresql"

    implementation project(":basilisk-starter")
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.zalando:logbook-spring-boot-starter"

    runtimeOnly "org.zalando:logbook-spring-boot-starter"
    runtimeOnly "net.logstash.logback:logstash-logback-encoder:6.1"
    runtimeOnly "org.jolokia:jolokia-core"

    testImplementation project(":basilisk-testing")

    integrationTestImplementation sourceSets.test.output

    databaseTestImplementation sourceSets.test.output

    liveTestImplementation sourceSets.test.output
}

jar {
    enabled = true
}

bootRun {
    systemProperties = System.properties as Map<String, ?>
}

springBoot {
    buildInfo()
}
